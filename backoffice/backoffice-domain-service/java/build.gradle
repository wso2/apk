/*
 * Copyright (c) 2022, WSO2 LLC. (https://www.wso2.com) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    alias libs.plugins.openapi.generator.plugin
}
apply from: "$projectDir/../../../common-gradle-scripts/java.gradle"
description = 'WSO2 APK Backoffice API Common Component'

sourceSets {
    main {
        java {
            srcDirs("src/gen/java")
        }
    }
}

dependencies {
    implementation libs.org.wso2.apk.apimgt.api
    implementation libs.org.wso2.apk.apimgt.impl
    implementation libs.org.wso2.apk.apimgt.rest.api.util
    implementation libs.org.wso2.apk.apimgt.user
    implementation libs.jackson.databind
    implementation libs.commons.io
    implementation libs.commons.lang
    implementation libs.commons.lang3
    implementation libs.commons.collection
    implementation libs.cxf.rt.frontend.jaxrs
    implementation libs.cxf.rt.rs.service.description
    implementation libs.hibernate.validator
    implementation libs.swagger.annotations
    implementation libs.json.simple
    implementation libs.json
    implementation libs.gson
    implementation libs.openfeign.feign.core
    implementation libs.openfeign.feign.gson
    implementation libs.openfeign.feign.okhttp
    implementation libs.snakeyaml
    implementation libs.guava
    implementation libs.jackson.dataformat.yaml
    implementation libs.httpcomponents.client
    compileOnly libs.jackson.annotations
}


openApiGenerate {
    generatorName = "jaxrs-cxf-cdi"
    apiPackage = "org.wso2.apk.apimgt.backoffice"
    modelPackage = "org.wso2.apk.apimgt.backoffice.dto"
    outputDir = "$projectDir".toString()
    inputSpec = "$projectDir/src/main/resources/backoffice-api.yaml".toString()
    modelNameSuffix = "DTO"
    skipOverwrite = true
    globalProperties = [
            apis  : "false",
            models: ""
    ]
}

tasks.register('cleanDTOs', Delete) {
    group 'build'
    description 'Clean auto generated DTOs'
    delete "$projectDir/src/gen/java/org/wso2/apk/apimgt/rest/api/backoffice/v1/dto".toString()
}

tasks.named('compileJava').configure {
    dependsOn 'cleanDTOs'
    dependsOn 'openApiGenerate'
}
